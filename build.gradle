apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: 'sonar-runner'
apply plugin: "jacoco"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "eu.lestard"
version = "0.4.0"

apply from: "maven_upload.gradle"

repositories {
    mavenLocal()
    mavenCentral()
    maven{
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    markdownDoclet
}


dependencies {

    markdownDoclet 'ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1'

    // testing
    testCompile "eu.lestard:assertj-javafx:0.3.0"
    testCompile "org.assertj:assertj-core:1.6.0"
    testCompile "junit:junit:4.11"
}


jacoco {
    toolVersion = "0.7.0.201403182114"
}

javadoc.options{
    links "http://docs.oracle.com/javase/8/docs/api/", "http://docs.oracle.com/javase/8/javafx/api"

    docletpath = configurations.markdownDoclet.files.asType(List)
    doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
    addStringOption("parse-timeout", "10")
}

sonarRunner {
    sonarProperties {
        properties["sonar.libraries"] += System.env.'JAVA_HOME' + "/jre/lib/ext/jfxrt.jar"
        property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/test.exec"
        property 'sonar.junit.reportsPath', "${buildDir}/test-results"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
